plugins {
    id 'idea'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'com.sergeybochkov'
version = '3.0'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.sergeybochkov.com/releases'
    }
}

dependencies {
    implementation 'com.sb:bdev-rt:1.0.7.RELEASE'

    implementation 'com.miglayout:miglayout-parent:11.4.2'
    implementation 'com.miglayout:miglayout-swing:11.4.2'
    implementation 'com.formdev:flatlaf:3.7'
    implementation 'com.formdev:flatlaf-extras:3.7'
    implementation 'com.formdev:flatlaf-fonts-inter:4.1'

    implementation 'ch.qos.logback:logback-classic:1.5.24'

    implementation platform('com.konghq:unirest-java-bom:4.7.0')
    implementation 'com.konghq:unirest-java-core'
    implementation 'com.konghq:unirest-modules-gson'
    implementation 'com.google.code.gson:gson:2.13.2'

    implementation project(':common')

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

application {
    mainClass = 'sb.bookshelf.app.App'
    applicationDefaultJvmArgs = ["-Xmx64m"]
    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        applicationDefaultJvmArgs += ["-Xdock:name=Bookshelf"]
    }
}

tasks.register("macApp") {
    dependsOn('build')
    doLast {
        def outDir = "$layout.buildDirectory/macApp" as File
        def appDir = "$outDir/Bookshelf.app" as File
        delete outDir
        project.file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/bookshelf.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}
