plugins {
    id 'idea'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.50.0'
}

group = 'com.sergeybochkov'
version = '3.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.sergeybochkov.com/releases'
    }
}

dependencies {
    implementation 'com.sb:bdev-rt:1.0.4.RELEASE'

    implementation 'com.miglayout:miglayout-parent:11.2'
    implementation 'com.miglayout:miglayout-swing:11.2'
    implementation 'com.formdev:flatlaf:3.2.5'
    implementation 'com.formdev:flatlaf-extras:3.2.5'

    implementation 'ch.qos.logback:logback-classic:1.4.14'

    implementation platform('com.konghq:unirest-java-bom:4.2.1')
    implementation 'com.konghq:unirest-java-core'
    implementation 'com.konghq:unirest-object-mappers-gson'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation project(':common')

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

application {
    mainClass = 'sb.bookshelf.app.App'
    applicationDefaultJvmArgs = ["-Xmx64m", "-Xdock:name=Bookshelf"]
}

task("macApp", dependsOn: 'build') {
    doLast {
        def outDir = "$buildDir/macApp" as File
        def appDir = "$outDir/Bookshelf.app" as File
        delete outDir
        project.file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/bookshelf.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}